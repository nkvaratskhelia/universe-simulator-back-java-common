plugins {
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.3'
    id 'org.owasp.dependencycheck' version '6.5.3'
}

group = 'com.example.universe.simulator'
version = '0.4.4'
sourceCompatibility = '17'

idea {
    module {
        downloadJavadoc = true
    }
}

test {
    useJUnitPlatform()
}

def coverageExclusions = []

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    reports {
        xml.required = true
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
    violationRules {
        rule {
            limit {
                minimum = 1
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'universe-simulator'
        property 'sonar.login', System.getenv('US_SONARQUBE_TOKEN')
        property 'sonar.coverage.exclusions', coverageExclusions
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
}

publishing {
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/nkvaratskhelia/universe-simulator-back-java-common'
            credentials {
                username = System.getenv('US_GITHUB_PACKAGES_USER')
                password = System.getenv('US_GITHUB_PACKAGES_TOKEN')
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.6.3'
    }
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-amqp'
    compileOnly 'org.springframework.boot:spring-boot-starter-json'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}
