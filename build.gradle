plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.sonarqube' version '4.2.0.3129'
    id 'org.owasp.dependencycheck' version '8.2.1'
    id 'io.freefair.lombok' version '8.0.1'
}

group = 'com.example.universe.simulator'
version = '2.2.0'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.1.0'
    }
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-amqp'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'org.springframework.boot:spring-boot-starter-json'
    compileOnly 'org.springframework.boot:spring-boot-starter-test'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

idea {
    module {
        downloadJavadoc = true
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'universe-simulator'
        property 'sonar.login', System.getenv('US_SONARQUBE_TOKEN')
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
}

publishing {
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/nkvaratskhelia/universe-simulator-back-java-common'
            credentials {
                username = System.getenv('US_GITHUB_PACKAGES_USER')
                password = System.getenv('US_GITHUB_PACKAGES_TOKEN')
            }
        }
    }
}

lombok { version = dependencyManagement.importedProperties['lombok.version'] }
