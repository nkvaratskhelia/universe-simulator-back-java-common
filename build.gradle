plugins {
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.3'
}

group = 'com.example.universe.simulator'
version = '0.1.1'
sourceCompatibility = '11'

idea {
    module {
        downloadJavadoc = true
    }
}

test {
    useJUnitPlatform()
}

jacocoTestCoverageVerification {
    afterEvaluate {
        jacocoExclusions(classDirectories)
    }

    violationRules {
        rule {
            limit {
                minimum = 1
            }
        }
    }
}

private jacocoExclusions(classDirectories) {
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [])
    }))
}

jacocoTestReport {
    afterEvaluate {
        jacocoExclusions(classDirectories)
    }
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', "https://sonarcloud.io"
        property 'sonar.organization', "universe-simulator"
        property 'sonar.login', System.getenv('US_SONARQUBE_TOKEN')
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.5.4'
    }
}

dependencies {
    compileOnly 'org.springframework.boot:spring-boot-starter-amqp'
    compileOnly 'org.springframework.boot:spring-boot-starter-json'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = System.getenv('US_GITHUB_PACKAGES_URL')
            credentials {
                username = System.getenv('US_GITHUB_PACKAGES_USER')
                password = System.getenv('US_GITHUB_PACKAGES_TOKEN')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}
